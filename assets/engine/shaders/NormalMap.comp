#version 430

layout (local_size_x = 16, local_size_y = 16) in;
layout (binding = 0, rgba32f) uniform writeonly image2D normalmap;

uniform sampler2D heightmap;
uniform int N;
uniform float strength;

void main()
{
	ivec2 x = ivec2(gl_GlobalInvocationID.xy);
	vec2 tex_coord = gl_GlobalInvocationID.xy / float(N);
	
	float texel_size = 1.0 / N;
	
	float z0 = texture(heightmap, tex_coord + vec2(-texel_size, -texel_size)).x;
	float z1 = texture(heightmap, tex_coord + vec2(0, -texel_size)).x;
	float z2 = texture(heightmap, tex_coord + vec2(texel_size, -texel_size)).x;
	float z3 = texture(heightmap, tex_coord + vec2(-texel_size, 0)).x;
	float z4 = texture(heightmap, tex_coord + vec2(texel_size, 0)).x;
	float z5 = texture(heightmap, tex_coord + vec2(-texel_size, texel_size)).x;
	float z6 = texture(heightmap, tex_coord + vec2(0, texel_size)).x;
	float z7 = texture(heightmap, tex_coord + vec2(texel_size, texel_size)).x;
	
	vec3 normal;
	
	normal.z = 1.0 / strength;
	normal.x = z0 + 2.0 * z3 + z5 - z2 - 2.0 * z4 - z7;
	normal.y = z0 + 2.0 * z1 + z2 - z5 - 2.0 * z6 - z7;
	
	imageStore(normalmap, x, vec4(normalize(normal) + 1.0 / 2.0, 1.0));
}